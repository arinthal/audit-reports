### [L-#] Users can claim their rewards after the claim period has ends

## Summary

Users can claim their rewards after the 90-day claim period ends, until the contest is closed by the admin.

## Vulnerability Details

The `Pot::claimCut` function allows users to claim their rewards. According to the documentation, users have 90 days to do so. If not all users claim their rewards within this period, the remaining pool will be redistributed between the admin and the users who have already claimed their rewards. However, the issue is that users can still claim rewards after the 90-day period, up until the contest is closed by the admin.

## Impact

The claiming period requirement could be broken.

## Proof of Concepts

The user calls the `Pot::claimCut` function after the 90-day period has ended and successfully claims their reward.

## Proof of code

Add the following code to the `TestMyCut.t.sol` file within the `TestMyCut` contract.

```solidity
    function testClaimAfterClaimPeriodEnded() public mintAndApproveTokens {
        vm.startPrank(user);
        contest = ContestManager(conMan).createContest(players, rewards, IERC20(ERC20Mock(weth)), 4);
        ContestManager(conMan).fundContest(0);
        vm.stopPrank();

        // moving the timestamp 90 days + 1 second forward
        vm.warp(block.timestamp + 90 days + 1);

        uint256 playerClaimedRewardBeforeClaim = ERC20Mock(weth).balanceOf(player1);
        uint256 playerCutBeforeClaim = Pot(contest).checkCut(player1);

        assertEq(playerCutBeforeClaim, 3);
        assertEq(playerClaimedRewardBeforeClaim, 0);

        vm.prank(player1);
        // claim after the designated period
        Pot(contest).claimCut();

        uint256 playerClaimedRewardAfterClaim = ERC20Mock(weth).balanceOf(player1);
        uint256 playerCutAfterClaim = Pot(contest).checkCut(player1);

        assertEq(playerCutAfterClaim, 0);
        assertEq(playerClaimedRewardAfterClaim, 3);
    }
```

## Tools Used

- Manual Review
- Foundry

## Recommended Mitigation

The `Pot::claimCut` function should check that the claim period is still active and then process the claim.

Possible solution (changes in `Pot.sol` file):

```diff
+   error Pot__ClaimPeriodHasEnded();
```

```diff
    function claimCut() public {
        address player = msg.sender;
        uint256 reward = playersToRewards[player];
+       if (block.timestamp > i_deployedAt + 90 days) {
+           revert Pot__ClaimPeriodHasEnded();
+       }
        if (reward <= 0) {
            revert Pot__RewardNotFound();
        }
        playersToRewards[player] = 0;
        remainingRewards -= reward;
        claimants.push(player);
        _transferReward(player, reward);
    }
```

### [M-#] Admin may fund the contest a second time

## Summary

The admin can fund the contest a second time, which will increase the contract's token balance, but it won't increase the rewards available to users.

## Vulnerability Details

The `ContestManager::fundContest` function allows admin to fund the contest rewards. When the admin calls the function more than once, the contract's token balance will increase, but will not increase the amount of remaining rewards to be distributed to users, which is stored in the `Pot::remainingRewards` variable. The pool of rewards for users, including any unclaimed amounts, will remain at the initial amount specified when the contest was created.

## Impact

Excess funds added to the contract will be locked at the contest's contract address and cannot be withdrawn.

## Proof of Concept

When the admin calls the `ContestManager::fundContest` function a second time, the contract's token balance increases, but the amount of rewards available to users remains unchanged.

## Proof of Code

Add the following code to the `TestMyCut.t.sol` file within the `TestMyCut` contract.

```solidity
    function testFundContestSecondTime() public mintAndApproveTokens {
        rewards = [500, 500];
        totalRewards = 1000;
        vm.startPrank(user);
        contest = ContestManager(conMan).createContest(players, rewards, IERC20(ERC20Mock(weth)), totalRewards);
        ContestManager(conMan).fundContest(0);

        uint256 initialContestTokenBalance = ERC20Mock(weth).balanceOf(contest);
        uint256 initialRemainingRewards = Pot(contest).getRemainingRewards();

        // admin funds the contest for second time
        ContestManager(conMan).fundContest(0);

        uint256 updatedContestTokenBalance = ERC20Mock(weth).balanceOf(contest);
        uint256 updatedRemainingRewards = Pot(contest).getRemainingRewards();

        // contest contract token balance doubled after second funding
        assertEq(initialContestTokenBalance * 2, updatedContestTokenBalance);

        // remaining rewards are the same after second funding
        assertEq(initialRemainingRewards, updatedRemainingRewards);

        assertLt(updatedRemainingRewards, updatedContestTokenBalance);
    }
```

## Tools Used

- Manual Review
- Foundry

## Recommended mitigation

One possible solution is to provide a mechanism for blocking the funding of a Pot which was already funded. A bool flag could be set after a contest is funded and prevent it from being funded again.

Changes to provide in `Pot.sol` file inside the `Pot` contract:

```diff
+   bool private isFunded;
```

```diff
+   function setFunded() external onlyOwner {
+       isFunded = true;
+   }
+
+   function checkIsFunded() public view returns (bool) {
+       return isFunded;
+   }
```

Changes to provide in `ContestManager.sol` file inside the `ContestManager` contract:

```diff
+   error ContestManager__ContestAlreadyFunded();
```

```diff
    function fundContest(uint256 index) public onlyOwner {
        Pot pot = Pot(contests[index]);
        IERC20 token = pot.getToken();
        uint256 totalRewards = contestToTotalRewards[address(pot)];

        if (token.balanceOf(msg.sender) < totalRewards) {
            revert ContestManager__InsufficientFunds();
        }

+       if (pot.checkIsFunded()) {
+           revert ContestManager__ContestAlreadyFunded();
+       }
+
+       pot.setFunded();

        token.transferFrom(msg.sender, address(pot), totalRewards);
    }
```

### [H-#] Incorrect calculation of remaining rewards for distribution among users

## Summary

The remaining rewards in the Pot are incorrectly calculated for distribution among users.

## Vulnerability Details

After the 90-day reward claiming period ends, the admin can close the reward pool using the `ContestManager::closeContest` function, which triggers the `Pot::closePot` function. If not all users have claimed their rewards, 10% of the remaining tokens in the pool are transferred to the contract manager, and the rest are distributed among the users who have claimed their rewards.
The error in the contract is that the remaining rewards, after subtracting the portion allocated to the contract manager, are divided by number of all users participating in the contest, but distributed only to those who have claimed their rewards.

```solidity
    function closePot() external onlyOwner {
        if (block.timestamp - i_deployedAt < 90 days) {
            revert Pot__StillOpenForClaim();
        }
        if (remainingRewards > 0) {
            uint256 managerCut = remainingRewards / managerCutPercent;
            i_token.transfer(msg.sender, managerCut);

@>          uint256 claimantCut = (remainingRewards - managerCut) / i_players.length;
@>          for (uint256 i = 0; i < claimants.length; i++) {
@>              _transferReward(claimants[i], claimantCut);
            }
        }
    }
```

## Impact

When not all eligible users claim their rewards during claim period, the incorrect distribution of the remaining rewards results in a smaller allocation for users who did claim their rewards. Additionally, part of the tokens remain in the contract, preventing their withdrawal.

## Proof of Concept

1. Three users are eligible to claim rewards (300 tokens each)
2. Two of them claim their rewards during the 90-day period, the third user's reward is not claimed (300 tokens)
3. After the claim period ends, the admin closes the Pot:
   - the contract manager receives 10% of the remaining rewards (30 tokens)
   - the rest (270 tokens) is divided by number of all participating users
   - rewards are transfered to users who claimed their rewards during the designated period ((300 - 30) / 3 = 90 tokens each), and 90 tokens remain at the contract account

## Proof of Code

Add the following code to the `TestMyCut.t.sol` file within the `TestMyCut` contract.

```solidity
    function testIncorrectDistributionOfRemainingRewardsAmongClaimers() public mintAndApproveTokens {
        address player3 = makeAddr("player3");
        players = [player1, player2, player3];
        rewards = [300, 300, 300];
        totalRewards = 900;

        vm.startPrank(user);
        contest = ContestManager(conMan).createContest(players, rewards, IERC20(ERC20Mock(weth)), totalRewards);
        ContestManager(conMan).fundContest(0);
        vm.stopPrank();

        vm.prank(player1);
        Pot(contest).claimCut();

        vm.prank(player2);
        Pot(contest).claimCut();

        // numOfClaimants - number of users between whom the protocol should divide the remaining rewards
        uint256 numOfClaimants = 2;
        uint256 player1ClaimedReward = ERC20Mock(weth).balanceOf(player1);

        vm.warp(block.timestamp + 90 days + 1);

        uint256 remainingRewards = Pot(contest).getRemainingRewards();

        vm.prank(user);
        ContestManager(conMan).closeContest(contest);

        uint256 managerCut = ERC20Mock(weth).balanceOf(conMan);

        // expected player1 balance after rewards distribution
        // player1ClaimedReward + (remainingRewards - managerCut)/numOfClaimants
        // 300 + (300 - 30) / 2 =  435
        uint256 player1ExpectedBalance = player1ClaimedReward + (remainingRewards - managerCut)/numOfClaimants;
        uint256 player1CurrentBalance = ERC20Mock(weth).balanceOf(player1);
        uint256 remainingFundsAfterClosing = ERC20Mock(weth).balanceOf(contest);

        assertLt(player1CurrentBalance, player1ExpectedBalance);
        assertGt(remainingFundsAfterClosing, 0);
    }
```

## Tools Used

- Manual Review
- Foundry

## Recommended Mitigation

The remaining portion of the reward, unclaimed by users, should be reduced by the portion for the contest manager, and then divided by the number of users who have previously claimed their rewards. To do this, make the following changes to the `Pot::closePot` function in the `Pot.sol` file:

```diff
    function closePot() external onlyOwner {
            if (block.timestamp - i_deployedAt < 90 days) {
                revert Pot__StillOpenForClaim();
            }
            if (remainingRewards > 0) {
                uint256 managerCut = remainingRewards / managerCutPercent;
                i_token.transfer(msg.sender, managerCut);

-               uint256 claimantCut = (remainingRewards - managerCut) / i_players.length;
+               uint256 claimantCut = (remainingRewards - managerCut) / claimants.length;
                for (uint256 i = 0; i < claimants.length; i++) {
                    _transferReward(claimants[i], claimantCut);
                }
            }
        }
```

### [M-#] Unchecked return of ERC-20 transfer

## Summary

Failed transfers of ERC-20 token's may result in loss of the admin or users funds.

## Vulnerability Details

The `Pot::_transferReward`, `Pot::closePot`, and `ContestManager::fundContest` methods allow ERC-20 tokens to be transferred between accounts using the ERC-20 methods `transfer` and `transferFrom`. However, different ERC-20 tokens handle success or failure differently. Some revert the transaction on failure, while others simply return `False`. On success, some return `True`, while others don't return any value. The issue in the audited contracts is that there is no mechanism to check if these method calls succeeded or failed.

In `Pot.sol` file:

```solidity
    function closePot() external onlyOwner {
        if (block.timestamp - i_deployedAt < 90 days) {
            revert Pot__StillOpenForClaim();
        }
        if (remainingRewards > 0) {
            uint256 managerCut = remainingRewards / managerCutPercent;
@>          i_token.transfer(msg.sender, managerCut);

            uint256 claimantCut = (remainingRewards - managerCut) / i_players.length;
            for (uint256 i = 0; i < claimants.length; i++) {
                _transferReward(claimants[i], claimantCut);
            }
        }
    }

    function _transferReward(address player, uint256 reward) internal {
@>      i_token.transfer(player, reward);
    }
```

In `ContestManager.sol` file:

```solidity
    function fundContest(uint256 index) public onlyOwner {
        Pot pot = Pot(contests[index]);
        IERC20 token = pot.getToken();
        uint256 totalRewards = contestToTotalRewards[address(pot)];

        if (token.balanceOf(msg.sender) < totalRewards) {
            revert ContestManager__InsufficientFunds();
        }

@>      token.transferFrom(msg.sender, address(pot), totalRewards);
    }
```

## Impact

If the transfer fails, the transaction will still be successful, but users won't claim their rewards or the admin won't fund the pot.

## Tools Used

- Manual Review
- Foundry

## Recommended Mitigation

A mechanism should be implemented to check whether and what data is returned by the methods used to transfer tokens (`transfer`/`transferFrom`). It can be implemented by using the OpenZeppelin `SafeERC20` library and using the `safeTransfer` and `safeTransferFrom` methods.

In `Pot.sol` file:

```diff
+import {SafeERC20} from "lib/openzeppelin-contracts/contracts/token/ERC20/utils/SafeERC20.sol";

+using SafeERC20 for IERC20;
```

```diff
    function closePot() external onlyOwner {
        if (block.timestamp - i_deployedAt < 90 days) {
            revert Pot__StillOpenForClaim();
        }
        if (remainingRewards > 0) {
            uint256 managerCut = remainingRewards / managerCutPercent;
-           i_token.transfer(msg.sender, managerCut);
+           i_token.safeTransfer(msg.sender, managerCut);

            uint256 claimantCut = (remainingRewards - managerCut) / i_players.length;
            for (uint256 i = 0; i < claimants.length; i++) {
                _transferReward(claimants[i], claimantCut);
            }
        }
    }

    function _transferReward(address player, uint256 reward) internal {
-       i_token.transfer(player, reward);
+       i_token.safeTransfer(player, reward);
    }
```

In `ContestManager.sol` file:

```diff
+import {SafeERC20} from "lib/openzeppelin-contracts/contracts/token/ERC20/utils/SafeERC20.sol";

+using SafeERC20 for IERC20;
```

```diff
    function fundContest(uint256 index) public onlyOwner {
        Pot pot = Pot(contests[index]);
        IERC20 token = pot.getToken();
        uint256 totalRewards = contestToTotalRewards[address(pot)];

        if (token.balanceOf(msg.sender) < totalRewards) {
            revert ContestManager__InsufficientFunds();
        }

-       token.transferFrom(msg.sender, address(pot), totalRewards);
+       token.safeTransferFrom(msg.sender, address(pot), totalRewards);
    }
```
